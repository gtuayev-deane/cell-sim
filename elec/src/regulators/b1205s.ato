from "generics/interfaces.ato" import Power
from "generics/capacitors.ato" import Capacitor
from "generics/leds.ato" import LEDIndicatorGreen

module B1205S_2WR2 from IsolatedRegulator:
    """
    Isolated 2W DC-DC converter
        -Input Voltage: 10.8V to 13.2V
        -Output Voltage: 5V
        -Output Current: 400mA
    """
    converter = new _B1205S_2WR2
    converter.power_in ~ power_in
    converter.power_out ~ power_out

    led = new LEDIndicatorGreen
    led.v_in = power_out.voltage
    led.power ~ power_out
    led.current = 0.05mA to 0.2mA

    input_cap = new Capacitor
    input_cap.value = 2.2uF +/- 10%
    input_cap.footprint = "C0805"
    power_in ~ input_cap.power

    output_cap = new Capacitor
    output_cap.value = 10uF +/- 10%
    output_cap.footprint = "C0805"
    power_out ~ output_cap.power


component _B1205S_2WR2:
    # 2W, 12V in 5V out
    footprint = "PWRM-TH_BXXXXS-2WR2"
    lcsc_id = "C2992386"
    mpn = "C2992386"
    # pins
    power_in = new Power
    power_in.vcc ~ pin 1
    power_in.gnd ~ pin 2
    power_in.voltage &= 10.8V to 13.2V

    power_out = new Power
    power_out.vcc ~ pin 6
    power_out.gnd ~ pin 4
    power_out.voltage &= 5V +/- 10%
    # power_out.current += 400mA